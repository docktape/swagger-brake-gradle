plugins {
    id 'groovy-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'signing'
    id 'org.jreleaser' version '1.22.0'
}

group 'com.docktape'
version '2.8.0-SNAPSHOT'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

gradlePlugin {
    website = 'https://docktape.com'
    vcsUrl = 'https://github.com/docktape/swagger-brake-gradle'

    plugins {
        swaggerBrake {
            id = 'com.docktape.swagger-brake'
            displayName = "Swagger Brake Gradle plugin"
            description = 'A plugin to check Swagger definition files for any breaking API changes. Originally was io.redskap.swagger-brake'
            tags.addAll('swagger', 'api', 'breaking-change', 'REST')
            implementationClass = 'com.docktape.swagger.brake.gradle.SwaggerBrakePlugin'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    implementation "com.docktape:swagger-brake:2.7.0"
    implementation "org.apache.commons:commons-collections4:4.5.0"
    implementation "org.apache.commons:commons-lang3:3.20.0"
    implementation 'com.google.guava:guava:32.1.3-jre'

    testImplementation "net.bytebuddy:byte-buddy:1.18.2"
    testImplementation "org.objenesis:objenesis:3.0.1"
    testImplementation("org.spockframework:spock-core:2.4-groovy-3.0") {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation gradleTestKit()
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        events("failed", "passed")
        exceptionFormat = "full"
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

if (!isReleaseBuild()) {
    publishing {
        repositories {
            maven {
                url = 'https://central.sonatype.com/repository/maven-snapshots/'

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

jreleaser {
    project {
        name = 'swagger-brake-gradle'
        description = 'Swagger contract checker for breaking API changes'
        website = 'https://github.com/docktape/swagger-brake-gradle'
        authors = ['Arnold Galovics']
        license = 'Apache-2.0'
        inceptionYear = '2018'

        links {
            homepage = 'https://github.com/docktape/swagger-brake-gradle'
        }
    }

    release {
        github {
            repoOwner = 'docktape'
            name = 'swagger-brake-gradle'
            overwrite = true
            changelog {
                formatted = 'ALWAYS'
            }
        }
    }
}