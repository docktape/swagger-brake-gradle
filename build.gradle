plugins {
    id 'groovy-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'signing'
}

group 'io.redskap'
version '2.5.0-SNAPSHOT'


tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gradlePlugin {
    website = 'http://redskap.io'
    vcsUrl = 'https://github.com/redskap/swagger-brake-gradle'

    plugins {
        swaggerBrake {
            id = 'io.redskap.swagger-brake'
            displayName = "Swagger Brake Gradle plugin"
            description = 'A plugin to check Swagger definition files for any breaking API changes'
            tags.addAll('swagger', 'api', 'breaking-change', 'REST')
            implementationClass = 'io.redskap.swagger.brake.gradle.SwaggerBrakePlugin'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    implementation "io.redskap:swagger-brake:2.4.0"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-lang3:3.13.0"
    implementation 'com.google.guava:guava:32.1.2-jre'

    testImplementation "net.bytebuddy:byte-buddy:1.9.12"
    testImplementation "org.objenesis:objenesis:3.0.1"
    testImplementation ("org.spockframework:spock-core:2.3-groovy-3.0") {
        exclude group:'org.codehaus.groovy'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    publishing {
        repositories {
            maven {
                url = isReleaseBuild() ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/' : 'https://oss.sonatype.org/content/repositories/snapshots/'

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}